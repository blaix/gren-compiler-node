module CLI.Parser exposing 
    ( App
    --
    , GroupParser
    , defineGroup
    , withCommand
    , withPrefix
    , run
    --
    , Command
    , CommandParseResult(..)
    --
    , ValueParser
    --
    , ArgumentParser
    , ArgumentParserError(..)
    , argumentErrorPrettified
    , noArgs
    , oneArg
    , twoArgs
    , threeArgs
    , zeroOrMoreArgs
    , mapArgs
    , oneOfArgs
    --
    , FlagParser
    , FlagParserError(..)
    , flagErrorPrettified
    , noFlags
    , initFlags
    , toggle
    , flag
    --
    , pathParser
    , grenFileParser
    )


import Dict exposing (Dict)
import FileSystem.Path as Path exposing (Path)
import CLI.PrettyPrinter as PP


type alias App result =
    { name : String
    , version : String
    , intro : PP.Document
    , outro : PP.Document
    , commands : GroupParser result
    }


type GroupParser result
    = GroupParser 
        (Dict String (Maybe String))
        (String -> String -> Array String -> Dict String String -> CommandParseResult result)


defineGroup : GroupParser result
defineGroup =
    GroupParser Dict.empty <| \_appName name _args _flags ->
        UnknownCommand name


withCommand : Command args flags result -> GroupParser result -> GroupParser result
withCommand command (GroupParser knownCommands next) =
    GroupParser (Dict.set command.word command.commonDescription knownCommands) <| \appName name args flags ->
        if name /= command.word then
            next appName name args flags

        else
            runCommand appName args flags command


withPrefix : String -> GroupParser result -> GroupParser result -> GroupParser result
withPrefix prefix prefixedCommands (GroupParser knownCommands nextCommand) =
    GroupParser (Dict.set prefix Nothing knownCommands) <| \appName name args flags ->
        if name /= prefix then
            nextCommand appName name args flags

        else
            runPrefix appName prefix args flags prefixedCommands


run : Array String -> App result -> CommandParseResult result
run tokens appDef =
    let
        (GroupParser _ parse) =
            appDef.commands
        
        { trues = flags, falses = arguments } =
            Array.partition (\word -> String.startsWith "--" word) tokens
    in
    case flags of
        [ "--version" ] ->
            HelpText <| PP.text appDef.version
        
        _ ->
            case Array.popFirst arguments of
                Just { first = command, rest = args } ->
                    parse appDef.name command args (buildFlagDict flags)

                Nothing ->
                    HelpText <| appHelpText appDef


appHelpText : App result -> PP.Document
appHelpText appDef =
    let
        (GroupParser knownCommands _) =
            appDef.commands
        
        commonCommands =
            knownCommands
                |> Dict.filterMap (\_name commonDescription -> commonDescription)

        knownCommandsDocument =
            PP.verticalBlock
                [ knownCommands
                    |> Dict.keys
                    |> Array.map (\command -> PP.text <| appDef.name ++ " " ++ command)
                    |> PP.verticalBlock
                    |> PP.indent
                    |> PP.color PP.Cyan
                , PP.empty
                , PP.block
                    [ PP.text "Adding the "
                    , PP.color PP.Cyan <| PP.text "--help"
                    , PP.text " flag gives you more details about a specific command."
                    ]
                ]

        commandBlock =
            if Dict.isEmpty commonCommands then
                PP.verticalBlock
                    [ PP.words "In order to do something useful, you need to give me a command. Here's a full list:"
                    , PP.empty
                    , knownCommandsDocument
                    ]
            else
                PP.verticalBlock
                    [ PP.words "The most common commands are:"
                    , PP.empty
                    , commonCommands
                        |> Dict.foldl
                            (\name description acc ->
                                Array.pushLast
                                    (PP.verticalBlock
                                        [ PP.intenseColor PP.Cyan <| PP.text (appDef.name ++ " " ++ name)
                                        , PP.indent <|
                                            PP.words description
                                        , PP.empty
                                        ]
                                    )
                                    acc
                            )
                            []
                        |> PP.verticalBlock
                        |> PP.indent
                    , PP.words "There are a bunch of other commands as well though. Here is a full list:"
                    , PP.empty
                    , knownCommandsDocument
                    ]
    in
    PP.verticalBlock
        [ appDef.intro
        , PP.empty
        , commandBlock
        , PP.empty
        , appDef.outro
        , PP.empty
        ]


runPrefix : String -> String -> Array String -> Dict String String -> GroupParser result -> CommandParseResult result
runPrefix appName prefixName arguments flags ((GroupParser _ parse) as groupParser) =
    case Array.popFirst arguments of
        Just { first = command, rest = args } ->
            parse appName command args flags

        Nothing ->
            HelpText <| prefixHelpText appName prefixName groupParser


prefixHelpText : String -> String -> GroupParser result -> PP.Document
prefixHelpText appName prefixName (GroupParser knownCommands _) =
    let
        knownCommandsDocument =
            PP.verticalBlock
                [ knownCommands
                    |> Dict.keys
                    |> Array.map (\command -> PP.text <| appName ++ " " ++ prefixName ++ " " ++ command)
                    |> PP.verticalBlock
                    |> PP.indent
                    |> PP.color PP.Cyan
                , PP.empty
                , PP.block
                    [ PP.text "Adding the "
                    , PP.color PP.Cyan <| PP.text "--help"
                    , PP.text " flag gives you more details about a specific command."
                    ]
                ]
    in
    PP.verticalBlock
        [ PP.words "This is just a prefix. In order to do something useful, you need to give me a command. Here's a full list:"
        , PP.empty
        , knownCommandsDocument
        , PP.empty
        ]

buildFlagDict : Array String -> Dict String String
buildFlagDict flags =
    Array.foldl
        (\rawFlag dict ->
            let
                flagParts =
                    rawFlag
                        |> String.dropLeft 2 -- the --prefix            
                        |> String.split "="
                        |> Array.takeFirst 2
            in
            case flagParts of
                [ key ] ->
                    Dict.set key "" dict

                [ key, value ] ->
                    Dict.set key value dict

                _ ->
                    dict
        )
        Dict.empty
        flags


-- COMMAND


type alias Command args flags result =
    { word : String
    , arguments : ArgumentParser args
    , flags : FlagParser flags
    , commonDescription : Maybe String
    , summary : String
    , example : PP.Document
    , builder : args -> flags -> result
    }


type CommandParseResult a
    = UnknownCommand String
    | BadFlags FlagParserError
    | BadArguments ArgumentParserError
    | HelpText PP.Document
    | Success a


runCommand : String -> Array String -> Dict String String -> Command args flags result -> CommandParseResult result
runCommand appName args flags spec =
    let
        (ArgumentParser _ parseArgs) =
            spec.arguments
    
        (FlagParser knownFlags parseFlags) =
            spec.flags
    in
    case Dict.get "help" flags of
        Just "" ->
            HelpText <| commandHelpText appName spec

        _ ->
            case
                { argResult = parseArgs args
                , flagResult = parseFlags knownFlags flags
                }
            of
                { argResult = Ok argValue, flagResult = Ok flagValues } ->
                    Success <| spec.builder argValue flagValues
    
                { argResult = Err err } ->
                    BadArguments err
    
                { flagResult = Err err } ->
                    BadFlags err


commandHelpText : String -> Command args flags result -> PP.Document
commandHelpText appName spec =
    let
        (ArgumentParser argumentType _) =
            spec.arguments
        
        (FlagParser knownFlags _) =
            spec.flags

        flagDocument =
            Dict.foldl
                (\flagName flagDescription acc ->
                    let
                        flagSuffix =
                            if flagDescription.title == "" then
                                ""

                            else
                                "=<" ++ flagDescription.title ++ ">"
                    in
                    Array.prepend acc
                        [ PP.text ("--" ++ flagName ++ flagSuffix)
                            |> PP.color PP.Cyan
                        , PP.words flagDescription.description
                            |> PP.indent
                        , PP.empty
                        ]
                )
                []
                knownFlags
                |> PP.verticalBlock
                |> PP.indent
    in
    PP.verticalBlock
        [ PP.words spec.summary
        , PP.empty
        , PP.words (appName ++ " " ++ spec.word ++ " " ++ argumentType)
            |> PP.indent
            |> PP.color PP.Cyan
        , PP.empty
        , spec.example
        , PP.empty
        , PP.words "You can customize this command with the following flags:"
        , PP.empty
        , flagDocument
        ]


-- ARGS


type ArgumentParser val
    = ArgumentParser String (Array String -> Result ArgumentParserError val)


type alias ValueParser val =
    { singular : String
    , plural : String
    , fn : (String -> Maybe val)
    , examples : Array String
    }


type ArgumentParserError
    = ArgumentParserWrongArity { expected : Int, actual : Int }
    | ArgumentParserInvalidArgument { argument : String, title : String, examples : Array String }


argumentErrorPrettified : ArgumentParserError -> PP.Document
argumentErrorPrettified err =
    case err of
        ArgumentParserWrongArity info ->
            PP.verticalBlock
                [ PP.words <| 
                    "I was expecting "
                        ++ String.fromInt info.expected
                        ++ " arguments, but seem to have received "
                        ++ String.fromInt info.actual
                        ++ "."
                , PP.empty
                ]

        ArgumentParserInvalidArgument info ->
            PP.verticalBlock
                [ PP.words "I'm having trouble with this argument:"
                , PP.empty
                , PP.text info.argument
                    |> PP.color PP.Red
                    |> PP.indent
                , PP.empty
                , PP.block
                    [ PP.words "It's supposed to be a"
                    , PP.text (" <" ++ info.title ++ "> ")
                        |> PP.color PP.Yellow
                    , PP.words "value, like one of these:"
                    ]
                , PP.empty
                , info.examples
                    |> Array.map PP.text
                    |> PP.verticalBlock
                    |> PP.color PP.Green
                    |> PP.indent
                , PP.empty
                ]


noArgs : ArgumentParser {}
noArgs =
    ArgumentParser "" <| \args ->
        if Array.length args /= 0 then
            Err <| ArgumentParserWrongArity { expected = 0, actual = (Array.length args) }

        else
            Ok {}


oneArg : ValueParser val -> ArgumentParser val
oneArg parseFn =
    ArgumentParser ("<" ++ parseFn.singular ++ ">") <| \args ->
        case args of
            [ arg ] ->
                parseFn.fn arg
                    |> Maybe.map Ok
                    |> Maybe.withDefault 
                        (Err <| 
                            ArgumentParserInvalidArgument
                                { argument = arg
                                , title = parseFn.singular
                                , examples = parseFn.examples
                                }
                        )

            _ ->
                Err <| ArgumentParserWrongArity { expected = 1, actual = Array.length args }


twoArgs : (a -> b -> c) -> ValueParser a -> ValueParser b -> ArgumentParser c
twoArgs combine firstValue secondValue =
    ArgumentParser ("<" ++ firstValue.singular ++ "> <" ++ secondValue.singular ++ ">") <| \args ->
        case args of
            [ first, second ] ->
                case { first = firstValue.fn first, second = secondValue.fn second } of
                    { first = Just firstOk, second = Just secondOk } ->
                        Ok <| combine firstOk secondOk

                    { first = Nothing } ->
                        Err <| ArgumentParserInvalidArgument
                            { argument = first
                            , title = firstValue.singular
                            , examples = firstValue.examples
                            }
                    
                    { second = Nothing } ->
                        Err <| ArgumentParserInvalidArgument
                            { argument = second
                            , title = secondValue.singular
                            , examples = secondValue.examples
                            }

            _ ->
                Err <| ArgumentParserWrongArity { expected = 2, actual = Array.length args }


threeArgs : (a -> b -> c -> d) -> ValueParser a -> ValueParser b -> ValueParser c -> ArgumentParser d
threeArgs combine firstValue secondValue thirdValue =
    ArgumentParser ("<" ++ String.join "> <" [firstValue.singular, secondValue.singular, thirdValue.singular] ++ ">") <| \args ->
        case args of
            [ first, second, third ] ->
                case { first = firstValue.fn first, second = secondValue.fn second, third = thirdValue.fn third } of
                    { first = Just firstOk, second = Just secondOk, third = Just thirdOk } ->
                        Ok <| combine firstOk secondOk thirdOk

                    { first = Nothing } ->
                        Err <| ArgumentParserInvalidArgument
                            { argument = first
                            , title = firstValue.singular
                            , examples = firstValue.examples
                            }
                    
                    { second = Nothing } ->
                        Err <| ArgumentParserInvalidArgument
                            { argument = second
                            , title = secondValue.singular
                            , examples = secondValue.examples
                            }

                    { third = Nothing } ->
                        Err <| ArgumentParserInvalidArgument
                            { argument = third
                            , title = thirdValue.singular
                            , examples = thirdValue.examples
                            }

            _ ->
                Err <| ArgumentParserWrongArity { expected = 3, actual = Array.length args }


zeroOrMoreArgs : ValueParser val -> ArgumentParser (Array val)
zeroOrMoreArgs parseFn =
    ArgumentParser parseFn.plural <| \args ->
        let
            parsedArgs =
                Array.filterMap parseFn.fn args
        in
        if Array.length args /= Array.length parsedArgs then
            Err <| ArgumentParserInvalidArgument
                { argument = String.join " " args
                , title = parseFn.plural
                , examples = parseFn.examples
                }

        else
            Ok parsedArgs


mapArgs : (a -> b) -> ArgumentParser a -> ArgumentParser b
mapArgs mapper (ArgumentParser desc fn) =
    ArgumentParser desc <| \args ->
        case fn args of
            Ok val ->
                Ok (mapper val)

            Err err ->
                Err err


oneOfArgs : Array (ArgumentParser val) -> ArgumentParser val
oneOfArgs argParsers =
    ArgumentParser "<one-of>" <| \args ->
        let
            parseResults =
                Array.map (\(ArgumentParser _ fn) -> fn args) argParsers
        in
        case Result.firstOk parseResults of
            Just result ->
                Ok result

            Nothing ->
                Array.first parseResults
                    |> Maybe.withDefault 
                        (Err <| ArgumentParserInvalidArgument
                            { argument = String.join " " args
                            , title = "<one-of>" 
                            , examples = []
                            }
                        )


-- FLAGS


type FlagParser val
    = FlagParser KnownFlags (KnownFlags -> Dict String String -> Result FlagParserError val)


type alias KnownFlags =
    Dict String FlagParserDescription


type alias FlagParserDescription =
    { title : String
    , description : String
    , examples : Array String
    }


type FlagParserError
    = FlagParserFoundValueOnToggle KnownFlags String
    | FlagParserMissingValue KnownFlags String
    | FlagParserInvalidValue KnownFlags String
    | FlagParserUnknownFlag KnownFlags String


flagErrorPrettified : FlagParserError -> PP.Document
flagErrorPrettified err =
    case err of
        FlagParserFoundValueOnToggle knownFlags flagName ->
            PP.verticalBlock
                [ PP.words "This flag doesn't take a value:"
                , PP.empty
                , PP.text ("--" ++ flagName)
                    |> PP.color PP.Red
                    |> PP.indent
                , PP.empty
                , PP.words "Try removing the value."
                , PP.empty
                ]

        FlagParserMissingValue knownFlags flagName ->
            PP.verticalBlock
                [ PP.words "This flag needs more information:"
                , PP.empty
                , PP.text ("--" ++ flagName)
                    |> PP.color PP.Red
                    |> PP.indent
                , describeFlagUse knownFlags flagName
                ]
        
        FlagParserInvalidValue knownFlags flagName ->
            PP.verticalBlock
                [ PP.words "This flag was passed an invalid value:"
                , PP.empty
                , PP.text ("--" ++ flagName)
                    |> PP.color PP.Red
                    |> PP.indent
                , describeFlagUse knownFlags flagName
                ]
        
        FlagParserUnknownFlag knownFlags flagName ->
            let
                otherFlags =
                    Dict.foldl
                        (\name desc doc ->
                            if desc.title == "" then
                                Array.pushLast (PP.text <| "--" ++ name) doc

                            else
                                Array.pushLast 
                                    (PP.text <| "--" ++ name ++ "=<" ++ desc.title ++ ">")
                                    doc
                        )
                        []
                        knownFlags

                suffix =
                    if Array.length otherFlags == 0 then
                        PP.empty

                    else
                        PP.verticalBlock
                            [ PP.empty
                            , PP.words "Maybe you meant one of these?"
                            , PP.empty
                            , PP.verticalBlock otherFlags
                                |> PP.indent
                                |> PP.color PP.Green
                            , PP.empty
                            ]
            in
            PP.verticalBlock
                [ PP.words "I don't recognize this flag:"
                , PP.empty
                , PP.text ("--" ++ flagName)
                    |> PP.color PP.Red
                    |> PP.indent
                , suffix
                ]


describeFlagUse : KnownFlags -> String -> PP.Document
describeFlagUse knownFlags flagName =
    case Dict.get flagName knownFlags of
        Just flagDescription ->
            let
                examples =
                    flagDescription.examples
                        |> Array.map (\exampleStr -> PP.text <| "--" ++ flagName ++ "=" ++ exampleStr)
            in
            PP.verticalBlock
                [ PP.empty
                , PP.block 
                    [ PP.words ("It neeeds a")
                    , PP.text (" <" ++ flagDescription.title ++ "> ")
                        |> PP.color PP.Yellow
                    , PP.words " like this:"
                    ]
                , PP.empty
                , PP.verticalBlock examples
                    |> PP.color PP.Green
                    |> PP.indent
                , PP.empty
                ]

        Nothing ->
            PP.empty


noFlags : FlagParser {}
noFlags =
    initFlags {}


initFlags : a -> FlagParser a
initFlags value =
    FlagParser Dict.empty <| \knownFlags remainingFlags -> 
        case Dict.first remainingFlags of
            Just unparsedFlag ->
                Err <| FlagParserUnknownFlag knownFlags unparsedFlag.key

            Nothing ->
                Ok value


toggle : String -> String -> FlagParser (Bool -> b) -> FlagParser b
toggle key desc (FlagParser knownFlags previousSteps) =
    let
        descriptor =
            { title = ""
            , description = desc
            , examples = []
            }
    in
    FlagParser (Dict.set key descriptor knownFlags) <| \flagDescriptions opts ->
        case previousSteps flagDescriptions (Dict.remove key opts) of
            Ok next ->
                case Dict.get key opts of
                    Just "" ->
                        Ok (next True)

                    Just _ ->
                        Err <| FlagParserFoundValueOnToggle flagDescriptions key

                    Nothing ->
                        Ok (next False)

            Err err ->
                Err err

flag : String -> ValueParser a -> String -> FlagParser (Maybe a -> b) -> FlagParser b
flag key valueParserFn desc (FlagParser knownFlags previousSteps) =
    let
        descriptor =
            { title = valueParserFn.singular
            , description = desc
            , examples = valueParserFn.examples
            }
    in
    FlagParser (Dict.set key descriptor knownFlags) <| \flagDescriptions opts ->
        case previousSteps flagDescriptions (Dict.remove key opts) of
            Ok next ->
                case Dict.get key opts of
                    Just "" ->
                        Err <| FlagParserMissingValue flagDescriptions key
                    
                    Just value ->
                        case valueParserFn.fn value of
                            Just parsedValue ->
                                Ok (next <| Just parsedValue)

                            Nothing ->
                                Err <| FlagParserInvalidValue flagDescriptions key

                    Nothing ->
                        Ok (next Nothing)

            Err err ->
                Err err


-- PARSERS


pathParser : ValueParser Path
pathParser =
    { singular = "path"
    , plural = "paths"
    , fn = Just << Path.fromPosixString
    , examples =
        [ "folder"
        , "folder/file.md"
        ]
    }


grenFileParser : ValueParser Path
grenFileParser =
    { singular = "gren-file"
    , plural = "gren-files"
    , fn = \input ->
        pathParser.fn input
            |> Maybe.andThen
                (\path ->
                    if path.extension /= "gren" then
                        Nothing
                
                    else
                        Just path
                )
    , examples = 
        [ "MyModule.gren"
        , "src/Main.gren"
        ]
    }
