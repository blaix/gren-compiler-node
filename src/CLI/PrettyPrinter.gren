module CLI.PrettyPrinter exposing
    ( Document
    --
    , empty
    , text
    , indent
    , block
    , verticalBlock
    --
    , Color(..)
    , color
    , intenseColor
    , stripColor
    --
    , ToStringOptions
    , defaultOptions
    , toString
    , toStringWithOptions
    )


import Math


type Document
    = Empty
    | Text String
    | Colorized { color : Color, intense : Bool, document : Document }
    | Indented Document
    | Block (Array Document)
    | VerticalBlock (Array Document)


empty : Document
empty =
    Empty


text : String -> Document
text str =
    Text str


indent : Document -> Document
indent doc =
    Indented doc


block : Array Document -> Document
block docs =
    Block docs


verticalBlock : Array Document -> Document
verticalBlock docs =
    VerticalBlock docs


-- COLORS


type Color
    = Black
    | Red
    | Green
    | Yellow 
    | Blue
    | Magenta
    | Cyan
    | White


color : Color -> Document -> Document
color clr doc =
    Colorized
        { color = clr
        , intense = False
        , document = doc
        }


intenseColor : Color -> Document -> Document
intenseColor clr doc =
    Colorized
        { color = clr
        , intense = True
        , document = doc
        }


stripColor : Document -> Document
stripColor doc =
    case doc of
        Empty ->
            doc
            
        Text _ ->
            doc
        
        Colorized { document = colorizedDocument } ->
            colorizedDocument
        
        Indented document ->
            Indented <| stripColor document
        
        Block docs ->
            Block <| Array.map stripColor docs
                
        VerticalBlock docs ->
            VerticalBlock <| Array.map stripColor docs


ansiColorCode : Color -> Bool -> String
ansiColorCode clr intense =
    let
        baseValue =
            case clr of
                Black -> 30
                Red -> 31
                Green -> 32
                Yellow -> 33
                Blue -> 34
                Magenta -> 35
                Cyan -> 36
                White -> 37

        appliedIntensity =
            if intense then
                baseValue + 60

            else
                baseValue
    in
    String.fromInt appliedIntensity


ansiColorEscapePrefix : String
ansiColorEscapePrefix =
    "\u{001b}["


-- RUN


toString : Document -> String
toString doc =
    toStringWithOptions
        { maxColumns = Math.maxSafeInteger
        , indentationSize = 4
        }
        doc


type alias ToStringOptions =
    { maxColumns : Int
    , indentationSize : Int
    }


defaultOptions : ToStringOptions
defaultOptions =
    { maxColumns = Math.maxSafeInteger
    , indentationSize = 4
    }


toStringWithOptions : ToStringOptions -> Document -> String
toStringWithOptions { maxColumns, indentationSize } doc =
    toStringHelper 
        "" 
        (\currentIndent -> String.repeat indentationSize " " ++ currentIndent) 
        maxColumns
        doc


toStringHelper : String -> (String -> String) -> Int -> Document -> String
toStringHelper indentPrefix nextIndent maxColumns doc =
    case doc of
        Empty ->
            ""
            
        Text str ->
            let
                indentedText =
                    indentPrefix ++ str
            in
            if String.length indentedText > maxColumns then
                indentedText
                    |> splitByLength maxColumns
                    |> String.join "\n"

            else
                indentedText
        
        Colorized { color = clr, intense, document } ->
            String.join "" <|
                [ ansiColorEscapePrefix ++ ansiColorCode clr intense ++ "m"
                , toStringHelper indentPrefix nextIndent maxColumns document
                , ansiColorEscapePrefix ++ "39m"
                ]
        
        Indented document ->
            toStringHelper (nextIndent indentPrefix) nextIndent maxColumns document
        
        Block docs ->
            case Array.popFirst docs of
                Nothing ->
                    ""

                Just { first, rest } ->
                    Array.pushFirst 
                        (toStringHelper indentPrefix nextIndent maxColumns first)
                        (Array.map (toStringHelper "" identity maxColumns) rest)
                        |> String.join ""
        
        VerticalBlock docs ->
            docs
                |> Array.map (toStringHelper indentPrefix nextIndent maxColumns)
                |> String.join "\n"


splitByLength : Int -> String -> Array String
splitByLength size str =
    splitByLengthHelper size str []


splitByLengthHelper : Int -> String -> Array String -> Array String
splitByLengthHelper size str acc =
    if str == "" then
        acc

    else
        let
            first = String.left size str
            rest = String.dropLeft size str
        in
        splitByLengthHelper size rest (Array.pushLast first acc)
