module CLI.PrettyPrinter exposing
    ( Document
    --
    , empty
    , text
    , indent
    , block
    , verticalBlock
    --
    , Color
    , plain
    , black
    , red
    , green
    , yellow
    , blue
    , magenta
    , cyan
    , white
    --
    , toString
    )


type Document
    = Empty
    | Text String
    | Colorized { color : Color, document : Document }
    | Indented Document
    | Block (Array Document)
    | VerticalBlock (Array Document)


empty : Document
empty =
    Empty


text : String -> Document
text str =
    Text str


indent : Document -> Document
indent doc =
    Indented doc


block : Array Document -> Document
block docs =
    Block docs


verticalBlock : Array Document -> Document
verticalBlock docs =
    VerticalBlock docs


-- COLORS


type Color
    = Default
    | Black
    | Red
    | Green
    | Yellow 
    | Blue
    | Magenta
    | Cyan
    | White


plain : Document -> Document
plain doc =
    Colorized
        { color = Default
        , document = doc
        }


black : Document -> Document
black doc =
    Colorized
        { color = Black
        , document = doc
        }


red : Document -> Document
red doc =
    Colorized
        { color = Red
        , document = doc
        }


green : Document -> Document
green doc =
    Colorized
        { color = Green
        , document = doc
        }


yellow : Document -> Document
yellow doc =
    Colorized
        { color = Yellow
        , document = doc
        }


blue : Document -> Document
blue doc =
    Colorized
        { color = Blue
        , document = doc
        }


magenta : Document -> Document
magenta doc =
    Colorized
        { color = Magenta
        , document = doc
        }


cyan : Document -> Document
cyan doc =
    Colorized
        { color = Cyan
        , document = doc
        }


white : Document -> Document
white doc =
    Colorized
        { color = White
        , document = doc
        }


ansiColorCode : Color -> String
ansiColorCode color =
    case color of
        Default -> "39"
        Black -> "90"
        Red -> "31"
        Green -> "32"
        Yellow -> "33"
        Blue -> "34"
        Magenta -> "35"
        Cyan -> "36"
        White -> "37"


ansiColorEscapePrefix : String
ansiColorEscapePrefix =
    "\u{001b}["


-- RUN


toString : Document -> String
toString doc =
    toStringHelper 
        "" 
        (\currentIndent -> String.repeat 4 " " ++ currentIndent) 
        doc


toStringHelper : String -> (String -> String) -> Document -> String
toStringHelper indentPrefix nextIndent doc =
    case doc of
        Empty ->
            ""
            
        Text str ->
            indentPrefix ++ str
        
        Colorized { color, document } ->
            String.join "" <|
                [ ansiColorEscapePrefix ++ ansiColorCode color ++ "m"
                , toStringHelper indentPrefix nextIndent document
                , ansiColorEscapePrefix ++ "39m"
                ]
        
        Indented document ->
            toStringHelper (nextIndent indentPrefix) nextIndent document
        
        Block docs ->
            case Array.popFirst docs of
                Nothing ->
                    ""

                Just { first, rest } ->
                    Array.pushFirst 
                        (toStringHelper indentPrefix nextIndent first)
                        (Array.map (toStringHelper "" identity) rest)
                        |> String.join ""
        
        VerticalBlock docs ->
            docs
                |> Array.map (toStringHelper indentPrefix nextIndent)
                |> String.join "\n"

