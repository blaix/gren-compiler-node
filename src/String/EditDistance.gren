module String.EditDistance exposing
    ( calculate
    , findMostSimilar
    )

{- Based on code from wikipedia article on [Levenshtein distance](https://en.wikipedia.org/wiki/Levenshtein_distance)
-}

calculate : String -> String -> Int
calculate left right =
    when String.popFirst left is
        Nothing ->
            String.count right

        Just { first = firstLeft, rest = restLeft } ->
            when String.popFirst right is
                Nothing ->
                    String.count left

                Just { first = firstRight, rest = restRight } ->
                    if firstLeft == firstRight then
                        calculate restLeft restRight

                    else
                        1 +
                            ([ calculate left restRight -- character inserted
                            , calculate restLeft right -- character deleted
                            , calculate restLeft restRight -- character replaced
                            ]
                                |> Array.minimum
                                |> Maybe.withDefault 0
                            )


findMostSimilar : { maxAmount : Int, maxDistance : Int, from : Array String } -> String -> Array String
findMostSimilar { maxAmount, maxDistance, from } str =
    let
        byDistance =
            from
                |> Array.map (\sample -> { distance = calculate sample str, sample = sample })
                |> Array.keepIf (\{ distance } -> distance <= maxDistance)

        minDistance =
            byDistance
                |> Array.map .distance
                |> Array.minimum
                |> Maybe.withDefault 0
    in
    byDistance
        |> Array.keepIf (\{ distance } -> distance == minDistance)
        |> Array.takeFirst maxAmount
        |> Array.map .sample
